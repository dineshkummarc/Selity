#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010 by internet Multi Server Control Panel
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
#
# @category		i-MSCP
# @copyright	2010 - 2012 by i-MSCP | http://i-mscp.net
# @author		Daniel Andreca <sci2tech@gmail.com>
# @version		SVN: $Id$
# @link			http://i-mscp.net i-MSCP Home Site
# @license		http://www.gnu.org/licenses/gpl-2.0.html GPL v2

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/..";
use lib "$FindBin::Bin/../PerlLib";
use lib "$FindBin::Bin/../PerlVendor";

use iMSCP::Debug;
use iMSCP::Boot;
use iMSCP::Dialog;

newDebug('imscp-net-interfaces-mngr.log');

our $command = {
				bzip2	=> {extension => 'bz2',		command => 'CMD_BZIP'},
				gzip	=> {extension => 'gz',		command => 'CMD_GZIP'},
				lzma	=> {extension => 'lzma',	command => 'CMD_LZMA'}
			};

sub backup_imscp_start_up {

	umask(027);
	iMSCP::Boot->new()->init({nolock => 'yes'});

	iMSCP::Boot->new()->lock('/tmp/imscp-backup-imscp.lock');

	0;
}

sub backup_imscp_shut_down {

	use iMSCP::Mail;

	my @warnings	= getMessageByType('WARNING');
	my @errors		= getMessageByType('ERROR');

	my $msg	 = "\nWARNINGS:\n"		. join("\n", @warnings)	. "\n" if @warnings > 0;
	$msg	.= "\nERRORS:\n"		. join("\n", @errors)	. "\n" if @errors > 0;
	iMSCP::Mail->new()->errmsg($msg) if ($msg);

	0;
}

sub backup_imscp_database {

	use POSIX;
	use iMSCP::Execute;
	use iMSCP::File;

	my $date		= strftime "%Y.%m.%d-%H:%M", localtime;
	my $zip			= $main::imscpConfig{'ZIP'};
	my $sqlFName	= "$main::imscpConfig{'BACKUP_FILE_DIR'}/$main::imscpConfig{DATABASE_NAME}-$date.sql";
	my $compressCmd = $main::imscpConfig{$command->{$zip}->{command}}." --force \'$sqlFName\'";

	my $database = iMSCP::Database->new(db => $main::imscpConfig{DATABASE_TYPE})->factory();

	if($database->can('dumpdb')){

		my ($rs, $stdout, $stderr);

		$rs = $database->dumpdb($main::imscpConfig{DATABASE_NAME}, $sqlFName);
		return $rs if $rs;

		my $file = iMSCP::File->new(filename => $sqlFName);
		$file->mode(0640) and return 1;
		$file->owner($main::imscpConfig{'ROOT_USER'}, $main::imscpConfig{'ROOT_GROUP'}) and return 1;

		$rs = execute($compressCmd, \$stdout, \$stderr);
		debug("$stdout") if $stdout;
		error("$stderr") if $stderr;
		error("Can not dump $main::imscpConfig{DATABASE_NAME}") if (!$stderr && $rs);
		return $rs if $rs;

		#$file = iMSCP::File->new(filename => $fName);
		#$file->mode(0644) and return 1;
		#$file->owner($main::imscpConfig{'ROOT_USER'}, $main::imscpConfig{'ROOT_GROUP'}) and return 1;

	} else {
		warning("Driver for $main::imscpConfig{DATABASE_TYPE} does not support dump");
	}

	0;
}

sub backup_imscp_engine {

	if ($main::imscpConfig{'BACKUP_IMSCP'} ne 'yes') {
		#warning('domain backups are disabled');
		return 0;
	}

	unless(
		exists $command->{$main::imscpConfig{'ZIP'}}
		&&
		exists $command->{$main::imscpConfig{'ZIP'}}->{extension}
		&&
		exists $command->{$main::imscpConfig{'ZIP'}}->{command}
	){
		error("Backup algorithm not supported: $main::imscpConfig{'ZIP'}");
		return 1;
	}

	use iMSCP::Dir;
	my $dir = iMSCP::Dir->new(dirname => $main::imscpConfig{'BACKUP_FILE_DIR'});
	$dir->make({
		mode	=> 0750,
		user	=> $main::imscpConfig{'ROOT_USER'},
		group	=> $main::imscpConfig{'ROOT_GROUP'}
	}) and return 1;

	# DB backup
	my $rs = backup_imscp_database();
	return $rs if $rs;

	$rs = backup_imscp_configs();
	return $rs if $rs;

	#remove old backups: >14 days old
	my ($stdout, $stderr);
	$rs = execute("find $main::imscpConfig{BACKUP_FILE_DIR}/* -maxdepth 0 -type f -mtime +14 -print | xargs -r /bin/rm -v", \$stdout, \$stderr);
	debug("$stdout") if $stdout;
	error("$stderr") if $stderr;
	error("Can not remove old backups backup") if (!$stderr && $rs);
	return $rs if $rs;

	return 0;
}

sub backup_imscp_configs{

	my $date		= strftime "%Y.%m.%d-%H:%M", localtime;
	my $tarName	= "$main::imscpConfig{BACKUP_FILE_DIR}/config-backup-$date.tar";
	my $fName	= "$tarName.$command->{$main::imscpConfig{ZIP}}->{extension}";
	my $bkpCmd	= "$main::imscpConfig{CMD_TAR} ".
					"--create ".
					"-C $main::imscpConfig{CONF_DIR} ".
					"--$main::imscpConfig{ZIP} ".
					"--exclude=?*[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9] ".
					"-p ".
					"--file=$fName .";

	my ($rs, $stdout, $stderr);
	$rs = execute($bkpCmd, \$stdout, \$stderr);
	debug("$stdout") if $stdout;
	error("$stderr") if $stderr;
	error("Can not create backup") if (!$stderr && $rs);
	return $rs if $rs;

	my $file = iMSCP::File->new(filename => $fName);
	$file->mode(0640) and return 1;
	$file->owner($main::imscpConfig{'ROOT_USER'}, $main::imscpConfig{'ROOT_GROUP'}) and return 1;

	return 0;
}

my $rs;

exit 1 if backup_imscp_start_up();

exit 1 if backup_imscp_engine();

exit 1 if backup_imscp_shut_down();

exit 0;
