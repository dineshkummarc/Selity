#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010 - 2011 by internet Multi Server Control Panel
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
#
# @category		i-MSCP
# @copyright	2010 - 2012 by i-MSCP | http://i-mscp.net
# @author		Daniel Andreca <sci2tech@gmail.com>
# @link			http://i-mscp.net i-MSCP Home Site
# @license		http://www.gnu.org/licenses/gpl-2.0.html GPL v2

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/..";
use lib "$FindBin::Bin/../PerlLib";

umask(027);

use iMSCP::Debug;
use iMSCP::Boot;
use iMSCP::Dialog;

newDebug('imscp-uninstall.log');

################################################################################
# Starting update process
#
# @return void
#
sub uninstall_start_up {

	iMSCP::Boot->new()->init();

	#enter silent mode
	silent(1);

	user_dialog();

	0;
}

################################################################################
# User dialog
#
# @return void
#
sub user_dialog {

	use iMSCP::Dialog;

	iMSCP::Dialog->factory()->set('yes-label','CONTINUE');
	iMSCP::Dialog->factory()->set('no-label','EXIT');
	iMSCP::Dialog->factory()->set('defaultno', '');
	if (iMSCP::Dialog->factory()->yesno(
					"\n
						Welcome to \\Z1i-MSCP version $main::imscpConfig{'Version'}\\Zn Un-Install Dialog.

						This program will uninstall i-MSCP system from your server.

						\\Zu\\Z1[WARNING]\\Zn
						All domain users and their accounts will be removed.

						\\Zu\\Z4[NOTE]\\Zn
						During the migration process some or all services might require to be shut down or restarted.

						You can stop this process by pushing \\Z1EXIT\\Z0 button
						To continue select \\Z1CONTINUE\\Z0 button"

					)
	){
		iMSCP::Dialog->factory()->msgbox(
					"\n
					\\Z1[NOTICE]\\Zn

					The update process was aborted by user..."
		);
		exit 0;
	}

	0;
}
################################################################################
# Shutting down setup process
#
# @return int 0 on success, other on failure
#
sub uninstall_shut_down {

	iMSCP::Dialog->factory()->msgbox(
					"\n
					\\Z1Congratulations!!!\\Zn

					You have uninstall i-MSCP successfully!\n\n

					"
	);

	0;
}


sub uninstall_engine {

	use iMSCP::Stepper;
	## Starting user dialog

	my @steps = (

		[\&uninstall_customers, 'Remove customers: '],
		[\&uninstallServers, 'i-MSCP server uninstall task: '],
		[\&uninstall_imscpd, 'i-MSCP daemons uninstall task: '],
		[\&uninstall_crontab, 'i-MSCP crontab uninstall task: '],
		[\&uninstall_logrotate, 'i-MSCP logrotate uninstall task: '],
		[\&uninstall_sql, 'i-MSCP sql uninstall task: '],
		[\&uninstall_system_users, 'i-MSCP system user uninstall task: '],
		[\&uninstall_system_dirs, 'i-MSCP system directory uninstall task: '],
	);
	my $rs = 0;
	my $step = 1;
	for (@steps){
		$rs |= step($_->[0], $_->[1], scalar @steps, $step);
		$step++;
	}
	iMSCP::Dialog->factory()->endGauge() if iMSCP::Dialog->factory()->needGauge();

	$rs;
}


sub uninstall_customers {

	use iMSCP::Boot;

	my $tables = {
		domain => 'domain_status', domain_aliasses => 'alias_status',
		subdomain => 'subdomain_status', subdomain_alias => 'subdomain_alias_status',
		mail_users => 'status'
	};

	# Set status as 'change'
	my $rs		= 0;
	my $error;
	my $database = iMSCP::Database->new()->factory();

	while (my ($table, $field) = each %$tables) {
		$error = $database->doQuery('dummy',
			"
				UPDATE
					$table
				SET
					$field = 'delete'
				;
			"
		);
		$rs = 1 if (ref $error ne 'HASH');
	}

	iMSCP::Boot->new()->unlock();

	my ($stdout, $stderr);
	$rs |= execute("perl $main::imscpConfig{'ENGINE_ROOT_DIR'}/imscp-rqst-mngr", \$stdout, \$stderr);
	debug("$stdout") if $stdout;
	error("$stderr") if $stderr;
	error("Error while rebuilding customers configuration files") if(!$stderr && $rs);
	iMSCP::Boot->new()->lock();

	$rs;
}

sub uninstallServers{

	use iMSCP::Dir;
	use FindBin;
	use iMSCP::Stepper;

	my ($rs, $file, $class, $server, $msg);

	my $dir	= iMSCP::Dir->new(dirname => "$main::imscpConfig{'ENGINE_ROOT_DIR'}/PerlLib/Servers");
	$rs		= $dir->get();
	return $rs if $rs;

	my @servers = $dir->getFiles();

	my $step = 1;
	startDetail();

	for(@servers){
		s/\.pm//;
		$file	= "Servers/$_.pm";
		$class	= "Servers::$_";
		require $file;
		$server	= $class->factory();
		$msg = "Performing uninstall tasks for ".uc($_)." server". ($main::imscpConfig{uc($_)."_SERVER"} ? ": ".$main::imscpConfig{uc($_)."_SERVER"} : '');
		$rs |= step(sub{ $server->uninstall() }, $msg, scalar @servers, $step) if($server->can('uninstall'));
		$step++;
	}

	endDetail();

	$rs;
}

sub uninstall_system_users{

	use Modules::SystemGroup;
	Modules::SystemGroup->new()->delSystemGroup($main::imscpConfig{MASTER_GROUP});

}

sub uninstall_system_dirs{

	my $rs = 0;

	use iMSCP::Dir;

	chdir("/");

	for (
		$main::imscpConfig{'CONF_DIR'},
		$main::imscpConfig{'USER_HOME_DIR'},
		$main::imscpConfig{'LOG_DIR'},
		$main::imscpConfig{'ROOT_DIR'}
	) {
		$rs |= iMSCP::Dir->new(dirname => $_)->remove();
	}

	$rs;

}

sub uninstall_sql{

	my $database = iMSCP::Database->factory();
	my $rs = 0;
	my $err;

	my $rdata = $database->doQuery('sqlu_name', 'SELECT DISTINCT(`sqlu_name`) FROM `sql_user`');

	if(ref $rdata ne 'HASH'){
		error("$rdata");
		$rs = 1;
	}

	foreach (keys %$rdata) {

		$database->doQuery( 'delete', "DROP USER ?@?", $rdata->{$_}->{sqlu_name}, 'localhost');
		$database->doQuery( 'delete', "DROP USER ?@?", $rdata->{$_}->{sqlu_name}, '%');

	}

	$rdata = $database->doQuery('sqld_name', 'SELECT `sqld_name` FROM `sql_database`');
	if(ref $rdata ne 'HASH'){
		error("$rdata");
		$rs = 1;
	}

	$database->doQuery('delete', "DROP DATABASE $rdata->{$_}->{sqld_name}") foreach (keys %$rdata);

	$database->doQuery('delete', "DROP USER ?@?", 'pma', 'localhost');
	$database->doQuery('delete', "DROP USER ?@?", 'pma', '%');

	$err = $database->doQuery('dummy', 'FLUSH PRIVILEGES');
	if (ref $err ne 'HASH'){
		error($err);
		$rs = 1;
	}

	$database->doQuery('delete', "DROP DATABASE $main::imscpConfig{DATABASE_NAME}");

	$rs;
}

sub uninstall_crontab{

	my $rs = 0;

	use iMSCP::File;

	$rs |= iMSCP::File->new(filename =>'/etc/cron.d/imscp')->delFile() if -f '/etc/cron.d/imscp';

	$rs;
}

sub uninstall_logrotate{

	my $rs = 0;

	use iMSCP::File;

	$rs |= iMSCP::File->new(filename =>'/etc/logrotate.d/imscp')->delFile() if -f '/etc/logrotate.d/imscp';

	$rs;

}

sub uninstall_imscpd{

	my $rs = 0;
	my ($stdout, $stderr);

	use iMSCP::File;
	use iMSCP::Execute;

	for ($main::imscpConfig{'CMD_IMSCPN'}, $main::imscpConfig{'CMD_IMSCPD'}) {
		# Do not process if the service is disabled
		next if(/^no$/i);

		my $filename		= $_;
		my ($serviceName)	= /.*\/([^\/]*)$/;

		# Services remove (Debian, Ubuntu)
		$rs = execute("/usr/sbin/update-rc.d -f $serviceName remove", \$stdout, \$stderr);
		debug("$stdout") if $stdout;
		error("$stderr") if $rs;

		$rs |= iMSCP::File->new(filename => $filename)->delFile();
	}

	$rs;
}

################################################################################
#                                Main program                                  #
################################################################################

uninstall_start_up() and exit 1;
uninstall_engine();
uninstall_shut_down();

exit 0;
